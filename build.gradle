plugins {
  alias(libs.plugins.spotless) apply false
}

allprojects {
  group = 'com.carrotsearch.randomizedtesting'
  version = '0.1.0-SNAPSHOT'
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'com.diffplug.spotless'

  repositories {
    mavenCentral()
  }

  java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    modularity.inferModulePath = true
  }

  tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
  }

  spotless {
    java {
      googleJavaFormat(libs.versions.googleJavaFormat.get())
    }
  }

  tasks.register('tidy') {
    dependsOn tasks.named('spotlessApply')
  }

  testing {
    suites {
      test {
        useJUnitJupiter(libs.versions.junit)
      }
    }
  }
}
